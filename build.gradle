plugins {
    id 'application'
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
}

group 'org.improving.workshop'
version '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
    maven {
        url "https://packages.confluent.io/maven"
    }
    maven { url 'https://jitpack.io' }
}

application {
    mainClassName = project.hasProperty("stream") ? project.getProperty("stream") : "NULL"
}

run {
    // configure tracing properties if enabled
    if ("true" == project.tracingEnabled) {
        systemProperty "otel.traces.exporter", 'otlp'
        systemProperty "otel.exporter.otlp.protocol", 'grpc'
        systemProperty "otel.exporter.otlp.endpoint", 'http://localhost:4317'
        systemProperty "otel.resource.attributes", "service.name=$mainClassName"
        systemProperty "otel.logs.exporter", 'none'
        systemProperty "otel.metrics.exporter", 'none'

        jvmArgs = ["-javaagent:opentelemetry-javaagent_1.25.0.jar"]
    }
}

dependencies {
    // https://jitpack.io/#schroedermatt/data-demo
    implementation 'com.github.schroedermatt.data-demo:mockdata:0.2.0'

    implementation 'org.apache.kafka:kafka-clients:7.7.1-ce'
    implementation 'org.apache.kafka:kafka-streams:7.7.1-ce'

    // included for the Serdes
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    implementation 'org.springframework.kafka:spring-kafka:3.3.1'

    implementation 'net.datafaker:datafaker:1.8.1'
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.slf4j:slf4j-api:2.0.16'
    implementation 'org.slf4j:slf4j-simple:2.0.16'

    implementation 'com.sun.mail:javax.mail:1.6.2'
    implementation 'io.opentelemetry:opentelemetry-api:1.29.0'

    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    testCompileOnly 'org.projectlombok:lombok:1.18.36'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'
    testImplementation 'org.apache.kafka:kafka-streams-test-utils:7.7.1-ce'

    testImplementation "org.junit.jupiter:junit-jupiter:$junit_version"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testImplementation "org.junit-pioneer:junit-pioneer:$junit_pioneer_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
}

test {
    useJUnitPlatform()
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}
